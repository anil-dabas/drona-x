plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.1'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'com.google.protobuf' version '0.9.4' apply false
}

group = 'com.drona-x.exchange'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

allprojects {
	repositories {
		mavenCentral()
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'com.google.protobuf'

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		implementation 'com.google.protobuf:protobuf-java:3.25.1'
	}

	protobuf {
		protoc {
			artifact = 'com.google.protobuf:protoc:3.25.1'
		}
		generateProtoTasks {
			all().each { task ->
				task.builtins {
					java {
						option 'lite'
					}
				}
			}
		}
	}

	sourceSets {
		main {
			proto {
				srcDir '../proto'
			}
		}
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

bootJar {
	enabled = false
}

jar {
	enabled = true
}